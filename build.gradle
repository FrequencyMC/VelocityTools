//file:noinspection GroovyAssignabilityCheck

plugins {
    id("java")
    id("checkstyle")
    id("com.github.spotbugs").version("5.0.13")
    id("org.cadixdev.licenser").version("0.6.1")
    id("com.github.johnrengelman.shadow").version("7.1.2")
}

setGroup("net.elytrium")
setVersion("1.1.9-frequency")

compileJava {
    getOptions().setEncoding("UTF-8")
}

java {
    setSourceCompatibility(JavaVersion.VERSION_11)
    setTargetCompatibility(JavaVersion.VERSION_11)
}

repositories {
    mavenCentral()

    maven {
        setName("papermc-repo")
        setUrl("https://papermc.io/repo/repository/maven-public/")
    }
    maven {
        setName("elytrium-repo")
        setUrl("https://maven.elytrium.net/repo/")
    }
}

dependencies {
    compileOnly("com.velocitypowered:velocity-api:3.2.0-SNAPSHOT")
    annotationProcessor("com.velocitypowered:velocity-api:3.2.0-SNAPSHOT")
    compileOnly("com.velocitypowered:velocity-proxy:3.2.0-SNAPSHOT") // From Elytrium Repo.
    implementation("net.elytrium:java-commons:1.1.0-SNAPSHOT")
    implementation("net.elytrium:fastprepare:1.0.4")

    // Needs for some velocity methods.
    compileOnly("io.netty:netty-codec:4.1.86.Final")
    compileOnly("it.unimi.dsi:fastutil-core:8.5.11")

    implementation("org.bstats:bstats-velocity:3.0.0")

    compileOnly("com.github.spotbugs:spotbugs-annotations:4.6.0")
}

shadowJar {
    getArchiveClassifier().set("")

    minimize()

    relocate("org.bstats", "net.elytrium.velocitytools.bstats")
    relocate("net.elytrium.fastprepare", "net.elytrium.velocitytools.fastprepare")
    relocate("net.elytrium.java.commons", "net.elytrium.velocitytools.commons")
}

license {
    setHeader(file("HEADER.txt"))
}

checkstyle {
    setToolVersion("9.2")
    setConfigFile(file("${this.getRootDir()}/config/checkstyle/checkstyle.xml"))
    setConfigProperties("configDirectory": "${this.getRootDir()}/config/checkstyle")

    // The build should immediately fail if we have errors.
    setMaxErrors(0)
    setMaxWarnings(0)
}

spotbugsMain {
    reports {
        html {
            getRequired().set(true)
            getOutputLocation().set(file("${this.getBuildDir()}/reports/spotbugs/main/spotbugs.html"))
            setStylesheet("fancy-hist.xsl")
        }
    }
}

sourceSets.main.getJava().srcDir(getTasks().register("generateTemplates", Copy) { task ->
    task.getInputs().properties("version": getVersion())

    task.from(file("src/main/templates"))
        .into(getLayout().getBuildDirectory().dir("generated/sources/templates"))
        .expand("version": getVersion())
}.map {
    it.getOutputs()
})

assemble.dependsOn(shadowJar)
